vocabulary <http://smolang.org/dtdw#> as dtdw {
	extends <http://www.w3.org/2001/XMLSchema#> as xsd
	extends  <http://w3id.org/pko#> as pko 
	extends <http://purl.org/net/p-plan#> as p-plan 
	extends <http://www.w3.org/ns/prov#> as prov 
	
	concept DeploymentProcess < pko:Procedure
	concept DeploymentStep < p-plan:Step
	concept DeploymentVariable < p-plan:Variable
	concept DeploymentArtifact < prov:Entity
	
	concept DigitalTwinDesign
	concept DigitalTwinRole
	concept DigitalTwinSystem  < prov:Collection
	concept DigitalTwinElement < prov:Entity
	
	concept LoadStep < DeploymentStep
	concept SimulateStep < DeploymentStep
	concept FullyCalibrateStep < DeploymentStep
	concept DeployStep < DeploymentStep
	concept CompareStep < DeploymentStep
	
	concept DiagramVariable < DeploymentVariable
	concept ModelVariable < DeploymentVariable
	concept DataVariable < DeploymentVariable
	
	concept ModelRole < DigitalTwinRole
	concept EnablerRole < DigitalTwinRole
	concept ServiceRole < DigitalTwinRole
	
	concept ParameterArtifact < DeploymentArtifact
	concept ModelArtifact < DeploymentArtifact
	concept DataArtifact < DeploymentArtifact
	concept SimulatorArtifact < DeploymentArtifact
	
	scalar property templateString [ 
		domain DeploymentStep 
		range xsd:string 
		functional
	]
	scalar property variableAvailableIn  [ 
		domain DeploymentVariable 
		range xsd:string 
		functional
	]
	scalar property availableIn  [ 
		domain DeploymentArtifact 
		range xsd:string 
		functional
	]
	scalar property executedAt [ 
		domain DeploymentArtifact 
		range xsd:integer 
		functional
	]

	scalar property order [ 
		domain DeploymentStep 
		range xsd:integer 
		functional
	]

	relation entity implement [ 
		from  DigitalTwinSystem 
		to DigitalTwinDesign 
		forward implements
	]
	relation entity role [ 
		from DigitalTwinDesign 
		to DigitalTwinRole 
		forward hasRole
	]
	relation entity fills [ 
		from DigitalTwinElement 
		to DigitalTwinRole 
		forward fillsRole
	]
	relation entity enable [ 
		from EnablerRole 
		to ServiceRole 
		forward enables
	]
	relation entity require [ 
		from EnablerRole 
		to ModelRole 
		forward requires
	]
	relation entity produce [ 
		from DeploymentStep
		to DeploymentVariable
		forward produces
	]
	relation entity consume [ 
		from DeploymentStep 
		to DeploymentVariable 
		forward consumes
	]
}